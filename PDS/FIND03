         MACRO
         MYMAC &TYPE=,&DD=,&DSN=
         LCLA  &I1,&I2
         AIF   (T'&TYPE EQ 'O').NOTYPE
S&SYSNDX DS    0C
         DC    AL2(E&SYSNDX-S&SYSNDX)  LENGTH OF THIS ENTRY
&I1      SETA  N'&DD
&I2      SETA  N'&DSN
         DC    AL2(&I1)                NUMBER OF DD'S
         DC    AL2(&I2)                NUMBER OF DSN'S
         DC    CL8'&TYPE'              TYPE NAME
DDS&SYSNDX DS  0C
         AIF   (T'&DD EQ 'O').LBL0020
&I1      SETA  1
.LBL0010 ANOP
         DC    CL8'&DD(&I1)'
         AIF   (&I1 EQ N'&DD).LBL0020
&I1      SETA  &I1+1
         AGO   .LBL0010
.LBL0020 ANOP
DDE&SYSNDX  DS 0C
DSNS&SYSNDX DS 0C
         AIF   (T'&DSN EQ 'O').LBL0040
&I2      SETA  1
.LBL0030 ANOP
         DC    CL44'&DSN(&I2)'
         AIF   (&I2 EQ N'&DSN).LBL0040
&I2      SETA  &I2+1
         AGO   .LBL0030
.LBL0040 ANOP
DSNE&SYSNDX DS 0C
E&SYSNDX    DS 0C
         MEXIT
.NOTYPE  MNOTE 12,'TYPE IS REQUIRED'
         MEND
*---------------------------------------------------------------------*
*                                                                     *
*      Define "types" searchable by the FIND|EDIT utility             *
*                                                                     *
*---------------------------------------------------------------------*
FIND03   CSECT

*        DEFINE  'CLIST'
         MYMAC TYPE=CLIST,                                             +
               DD=(SYSPROC,ISPCLIB),                                   +
               DSN=(SYS1.MVSCMD)


*        DEFINE  'PROC'
         MYMAC TYPE=PROC,                                              +
               DSN=(SYS1.IPROCLIB,SYS1.PROCLIB)


*        DEFINE  'MACRO'
         MYMAC TYPE=MACRO,                                             +
               DSN=(SYS1.MACLIB,SYS1.MODGEN)
*---------------------------------------------------------------------*
         DC    X'FF'
         END   FIND03
