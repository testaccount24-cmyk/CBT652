* ------------------------------------------------------------------- *
*                                                                     *
*     This program searchs the TIOT for a given DDname.               *
*                                                                     *
*     If the DDname is found in the TIOT, the data set name(s)        *
*     allocated to the DD (up to 20) will be returned.                *
*                                                                     *
*     The parameters are passed via an area mapped by FINDREQ.        *
*                                                                     *
* ------------------------------------------------------------------- *
FIND02   CSECT
FIND02   AMODE 24
FIND02   RMODE 24
         USING FIND02,R12
         USING FIND02,R11
         STM   R14,R12,12(R13)       SAVE REGS
         LR    R12,R15               SET BASE REG
         B     FIND0000              SKIP EYECATCHER
         DC    CL8'FIND02'
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
FIND0000 DS    0H
         LR    R11,R1                SAVE PARM ADDRESS
         USING FINDREQ,R11           DEFINE BASE
         GETMAIN RU,LV=WORKL         OBTAIN WORK AREA
         ST    R13,4(R1)             CHAIN CALLER'S SAVE AREA TO OURS
         ST    R1,8(R13)             CHAIN OUR SAVE AREA TO CALLER'S
         LR    R13,R1                SET SAVE AREA ADDRESS
         USING WORK,R13              DEFINE BASE
* ------------------------------------------------------------------- *
*         SCAN TIOT                                                   *
* ------------------------------------------------------------------- *
         MVI   FINDSTAT,X'00'        SET RETURN CODE
         LA    R10,FINDDSNM          DATASET NAME(S)
         LH    R9,FINDNBR            MAXIMUM DD'S TO RETURN
         LTR   R9,R9                 NUMBER TO RETURN ZERO?
         BZ    ERR0010               YES
         XC    FINDNBR,FINDNBR       SET RETURNED TO ZERO
         USING PSA,R0                DEFINE BASE
         L     R1,PSATNEW            MY TCB'S ADDRESS
         USING TCB,R1                DEFINE TCB BASE
         L     R2,TCBTIO             TIOT ADDRESS
         USING TIOT1,R2              DEFINE BASE
         LA    R3,TIOENTRY           FIRST TIOT ENTRY
         USING TIOENTRY,R3           DEFINE BASE
         SR    R4,R4                 CLEAR FOR LENGTHS
FIND0010 DS    0H
         ICM   R4,1,TIOELNGH         LENGTH OF THIS ENTRY
         BZ    ERR0020               END OF TABLE
         CLC   TIOEDDNM,FINDDDNM     CORRECT DDNAME?
         BE    FIND0030              YES
         AR    R3,R4                 NEXT
         B     FIND0010              LOOP
FIND0020 DS    0H
         ICM   R4,1,TIOELNGH         LENGTH OF THIS ENTRY
         BZ    EXIT0000              END OF TABLE
         CLC   TIOEDDNM,BLANKS       BLANK DD NAME?
         BNE   EXIT0000              NO
FIND0030 DS    0H
         MVI   FINDSTAT,$FINDOK      THINGS ARE LOOKING GOOD
         BCT   R9,FIND0040           ONE LESS SLOT AVAILABLE
         MVI   FINDSTAT,$FINDOVF     OUTPUT AREA OVERFLOWED
         B     EXIT0000
FIND0040 DS    0H
         SR    R1,R1                 CLEAR REGISTER
         ICM   R1,7,TIOEJFCB         JFCB ADDRESS
         LA    R1,16(,R1)            ADJUST THE ADDRESS
         USING INFMJFCB,R1           DEFINE BASE
         MVC   0(L'FINDDSNM,R10),0(R1) COPY DATASET NAME
         LA    R10,L'FINDDSNM(,R10)  NEXT IN OUTPUT
         LH    R1,FINDNBR            NUMBER RETURNED
         LA    R1,1(,R1)             PLUS 1
         STH   R1,FINDNBR            UPDATE THE NUMBER
         AR    R3,R4                 NEXT TIOT ENTRY
         B     FIND0020              LOOP
ERR0010  DS    0H
         MVI   FINDSTAT,X'FF'        ERROR IN PARAMETERS
         B     EXIT0000              EXIT
ERR0020  DS    0H
         MVI   FINDSTAT,$FINDNF      DDNAME NOT FOUND
EXIT0000 DS    0H
         LR    R1,R13
         L     R13,4(R13)            RESTORE REGISTER 13                ASE01670
         FREEMAIN RU,A=(1),LV=WORKL
         LM    R14,R12,12(R13)       RESTORE ALL OTHER REGISTERS        ASE01680
         SR    R15,R15               GIVE GOOD RETURN CODE              ASE01690
         BR    R14                   RETURN TO CALLER                   ASE01700
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
BLANKS   DC    CL8' '
*---------------------------------------------------------------------*
*                                                                     *
*              WORK AREAS                                             *
*                                                                     *
*---------------------------------------------------------------------*
WORK     DSECT
WORKSAVE DS    18F
WORKL    EQU   *-WORK
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         FINDREQ DSECT=YES
*---------------------------------------------------------------------*
*              EQUATES                                                *
*---------------------------------------------------------------------*
         COPY REGEQU
         IHAPSA LIST=NO
         IKJTCB LIST=NO
         IEFTIOT1
JFCB     DSECT
         IEFJFCBN LIST=YES
         END  FIND02
